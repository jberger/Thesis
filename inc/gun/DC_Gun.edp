int 
  C1=1, // Label for grounded segments
  C2=5, // Label for HV segment(s)
  C3=10; // Label for vacuum segments
real 
  D=1, // Larger ID
  L=0.25, // Separation of plates
  R=0.1, // Radius of curvature of anode
  d=2*R, // Anode ID
  lf=0.2, // Placement of back plate behind end of curved segment
  l=(D-2*R-d)/2, // Calculated remaining distance
  pts=50, // Number of point PER UNIT LENGTH on the edges
  V=-10; // Voltage on cathode

border AnodeTop(t=0,1) 
 {x=L; y=D-l+l*t; label=C1;}
border GapTop(t=1,0) 
 {x=L*t; y=D; label=C3;}
border CathodeTop(t=0,1) 
 {x=0; y=D-l*t; label=C2;}
border CathodeRadiusTop(t=0,-pi/2) 
 {x=R*cos(t)-R; y=D-l+R*sin(t); label=C2;}
border CathodeBoreBack(t=0,1) 
 {x=R*cos(pi/2)-R; y=D-l-R-d*t; label=C2;}
border CathodeRadiusBottom(t=-pi/2,0) 
 {x=R*cos(t)-R; y=l-R*sin(t); label=C2;}
border CathodeBottom(t=1,0) 
 {x=0; y=l*t; label=C2;}
border GapBottom(t=0,1) 
 {x=L*t; y=0; label=C3;}
border AnodeBottom(t=0,1) 
 {x=L; y=l*t; label=C1;}
border AnodeRadiusBottom(t=pi,pi/2) 
 {x=L+R+R*cos(t); y=l+R*sin(t); label=C1;}
border AnodeBoreBottom(t=0,1) 
 {x=L+R+lf*t; y=l+R; label=C1;}
border AnodeBoreBack(t=0,1) 
 {x=L+R+lf; y=l+R+d*t; label=C1;}
border AnodeBoreTop(t=1,0) 
 {x=L+R+lf*t; y=l+R+d; label=C1;}
border AnodeRadiusTop(t=1.5*pi,pi) 
 {x=L+R+R*cos(t); y=l+2*R+d+R*sin(t); label=C1;}

plot(
  AnodeTop(pts*l)
  +GapTop(pts*L)
  +CathodeTop(pts*l)
  +CathodeRadiusTop(pts*R*pi/2)
  +CathodeBoreBack(pts*d)
  +CathodeRadiusBottom(pts*R*pi/2)
  +CathodeBottom(pts*l)
  +GapBottom(pts*L)
  +AnodeBottom(pts*l)
  +AnodeRadiusBottom(pts*R*pi/2)
  +AnodeBoreBottom(pts*lf)
  +AnodeBoreBack(pts*d)
  +AnodeBoreTop(pts*lf)
  +AnodeRadiusTop(pts*R*pi/2)
  ,
  wait=true
);

mesh Th=buildmesh(
  AnodeTop(pts*l)
  +GapTop(pts*L)
  +CathodeTop(pts*l)
  +CathodeRadiusTop(pts*R*pi/2)
  +CathodeBoreBack(pts*d)
  +CathodeRadiusBottom(pts*R*pi/2)
  +CathodeBottom(pts*l)
  +GapBottom(pts*L)
  +AnodeBottom(pts*l)
  +AnodeRadiusBottom(pts*R*pi/2)
  +AnodeBoreBottom(pts*lf)
  +AnodeBoreBack(pts*d)
  +AnodeBoreTop(pts*lf)
  +AnodeRadiusTop(pts*R*pi/2)
);
plot(Th,wait=true);

fespace Vh(Th,P2);
Vh Phi,w;

solve Laplace(Phi,w)=
  int2d(Th)(dx(Phi)*dx(w)+dy(Phi)*dy(w))
  +on(C2,Phi=V)
  +on(C1,Phi=0)
  +on(C3,Phi=V*(1-x/L));
plot(Phi);

{
  ofstream file("output.dat");
  for (int j=-pts*d/2; j<=pts*d/2; j++) {
    for (int i=0; i<=pts*(L+R+lf); i++){
      file << (j/pts) << " " << (i/pts) << " " << Phi(i/pts,l+R+d/2+j/pts) << endl;
    }
    file << endl;
  }
}

{
  ofstream file("onaxis.dat");
  for (int i=0; i<=pts*(L+R+lf); i++){
    file << (i/pts) << " " << Phi(i/pts,0) << " " << ((Phi((i+1)/pts,0) - Phi(i/pts,0))*pts) << endl;
  }
}
